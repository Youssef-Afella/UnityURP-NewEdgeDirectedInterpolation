#pragma kernel CopyAndDiagonalPass
#pragma kernel AxialPass

int WindowSize;
float Regulate;

Texture2D<float> InputTex;
RWTexture2D<float> ResultTex;

//from : https://gist.github.com/mattatz/86fff4b32d198d0928d0fa4ff32cf6fa
float4x4 inverse(float4x4 m)
{
    float n11 = m[0][0], n12 = m[1][0], n13 = m[2][0], n14 = m[3][0];
    float n21 = m[0][1], n22 = m[1][1], n23 = m[2][1], n24 = m[3][1];
    float n31 = m[0][2], n32 = m[1][2], n33 = m[2][2], n34 = m[3][2];
    float n41 = m[0][3], n42 = m[1][3], n43 = m[2][3], n44 = m[3][3];

    float t11 = n23 * n34 * n42 - n24 * n33 * n42 + n24 * n32 * n43 - n22 * n34 * n43 - n23 * n32 * n44 + n22 * n33 * n44;
    float t12 = n14 * n33 * n42 - n13 * n34 * n42 - n14 * n32 * n43 + n12 * n34 * n43 + n13 * n32 * n44 - n12 * n33 * n44;
    float t13 = n13 * n24 * n42 - n14 * n23 * n42 + n14 * n22 * n43 - n12 * n24 * n43 - n13 * n22 * n44 + n12 * n23 * n44;
    float t14 = n14 * n23 * n32 - n13 * n24 * n32 - n14 * n22 * n33 + n12 * n24 * n33 + n13 * n22 * n34 - n12 * n23 * n34;

    float det = n11 * t11 + n21 * t12 + n31 * t13 + n41 * t14;
    float idet = 1.0f / det;

    float4x4 ret;

    ret[0][0] = t11 * idet;
    ret[0][1] = (n24 * n33 * n41 - n23 * n34 * n41 - n24 * n31 * n43 + n21 * n34 * n43 + n23 * n31 * n44 - n21 * n33 * n44) * idet;
    ret[0][2] = (n22 * n34 * n41 - n24 * n32 * n41 + n24 * n31 * n42 - n21 * n34 * n42 - n22 * n31 * n44 + n21 * n32 * n44) * idet;
    ret[0][3] = (n23 * n32 * n41 - n22 * n33 * n41 - n23 * n31 * n42 + n21 * n33 * n42 + n22 * n31 * n43 - n21 * n32 * n43) * idet;

    ret[1][0] = t12 * idet;
    ret[1][1] = (n13 * n34 * n41 - n14 * n33 * n41 + n14 * n31 * n43 - n11 * n34 * n43 - n13 * n31 * n44 + n11 * n33 * n44) * idet;
    ret[1][2] = (n14 * n32 * n41 - n12 * n34 * n41 - n14 * n31 * n42 + n11 * n34 * n42 + n12 * n31 * n44 - n11 * n32 * n44) * idet;
    ret[1][3] = (n12 * n33 * n41 - n13 * n32 * n41 + n13 * n31 * n42 - n11 * n33 * n42 - n12 * n31 * n43 + n11 * n32 * n43) * idet;

    ret[2][0] = t13 * idet;
    ret[2][1] = (n14 * n23 * n41 - n13 * n24 * n41 - n14 * n21 * n43 + n11 * n24 * n43 + n13 * n21 * n44 - n11 * n23 * n44) * idet;
    ret[2][2] = (n12 * n24 * n41 - n14 * n22 * n41 + n14 * n21 * n42 - n11 * n24 * n42 - n12 * n21 * n44 + n11 * n22 * n44) * idet;
    ret[2][3] = (n13 * n22 * n41 - n12 * n23 * n41 - n13 * n21 * n42 + n11 * n23 * n42 + n12 * n21 * n43 - n11 * n22 * n43) * idet;

    ret[3][0] = t14 * idet;
    ret[3][1] = (n13 * n24 * n31 - n14 * n23 * n31 + n14 * n21 * n33 - n11 * n24 * n33 - n13 * n21 * n34 + n11 * n23 * n34) * idet;
    ret[3][2] = (n14 * n22 * n31 - n12 * n24 * n31 - n14 * n21 * n32 + n11 * n24 * n32 + n12 * n21 * n34 - n11 * n22 * n34) * idet;
    ret[3][3] = (n12 * n23 * n31 - n13 * n22 * n31 + n13 * n21 * n32 - n11 * n23 * n32 - n12 * n21 * n33 + n11 * n22 * n33) * idet;

    return ret;
}

[numthreads(8, 8, 1)]
void CopyAndDiagonalPass(uint3 id : SV_DispatchThreadID)
{
    //Copy Pixels /////////////////////////////////////////////////////////////
    ResultTex[id.xy * 2] = InputTex[id.xy].r;

    //Compute Diagonal Pixels /////////////////////////////////////////////////
    //computing both R = transpose(C) * C and r = transpose(C) * y
    //we can compute both of them directly without constructing the C matrix first
    float4x4 R = 0;
    float4 r = 0;
    
    int s = WindowSize >> 1;
    
    for (int i = -s; i < s; i++)
    {
        for (int j = -s; j < s; j++)
        {
            int2 pivot = id.xy + int2(i, j);
            
            float y = InputTex[pivot].r;
            
            float c1 = InputTex[pivot + int2(-1, -1)].r;
            float c2 = InputTex[pivot + int2(1, -1)].r;
            float c3 = InputTex[pivot + int2(1, 1)].r;
            float c4 = InputTex[pivot + int2(-1, 1)].r;
            
            float4 cr = float4(c1, c2, c3, c4);
            float4x4 outer = float4x4(cr.x * cr, cr.y * cr, cr.z * cr, cr.w * cr);
            
            R += outer;
            r += cr * y;
        }
    }
    
    float weight = 1.0 / float(s << 2);
    R *= weight;
    r *= weight;
    
    //Adding a small epsilon to the diagonal to avoid null determinant
    //(Not a great solution thought but otherwise we get some noise at the output in some cases)
    R[0][0] += Regulate;
    R[1][1] += Regulate;
    R[2][2] += Regulate;
    R[3][3] += Regulate;
    
    float4 alpha = saturate(mul(inverse(R), r));
    
    //Normalizing the weights
    float sum = alpha[0] + alpha[1] + alpha[2] + alpha[3]; 
    
    if (sum == 0)
    {
        alpha = 0.25;
    }
    else
    {
        alpha /= sum;
    }
    
    
    float4 n = float4(InputTex[id.xy].r,
                      InputTex[id.xy + int2(1, 0)].r,
                      InputTex[id.xy + int2(1, 1)].r,
                      InputTex[id.xy + int2(0, 1)].r);
    float result = dot(alpha, n);
    
    ResultTex[id.xy * 2 + 1] = result;
}

[numthreads(8, 8, 1)]
void AxialPass(uint3 id : SV_DispatchThreadID)
{
    //Compute Axial Pixels /////////////////////////////////////////////////
    //Same things as diagonals just with different neightbours and we use the same weights for both (2i + 1, 2j) and (2i, 2j + 1)
    float4x4 R = 0;
    float4 r = 0;
    
    int s = WindowSize >> 1;
    
    for (int i = -s; i < s; i++)
    {
        for (int j = -s; j < s; j++)
        {
            int2 pivot = (id.xy + int2(i, j)) * 2;
            
            float y = ResultTex[pivot + int2(1, -1)].r;
            
            float c1 = ResultTex[pivot + int2(-1, -1)].r;
            float c2 = ResultTex[pivot + int2(1, -3)].r;
            float c3 = ResultTex[pivot + int2(3, -1)].r;
            float c4 = ResultTex[pivot + int2(1, 1)].r;
            
            float4 cr = float4(c1, c2, c3, c4);
            float4x4 outer = float4x4(cr.x * cr, cr.y * cr, cr.z * cr, cr.w * cr);
            
            R += outer;
            r += cr * y;
        }
    }
    
    float weight = 1.0 / float(s << 2);
    R *= weight;
    r *= weight;
    
    R[0][0] += Regulate;
    R[1][1] += Regulate;
    R[2][2] += Regulate;
    R[3][3] += Regulate;
    
    int2 pos = id.xy * 2;
    
    float4 n1 = float4(ResultTex[pos].r,
                       ResultTex[pos + int2(1, -1)].r,
                       ResultTex[pos + int2(2, 0)].r,
                       ResultTex[pos + int2(1, 1)].r );
    
    float4 n2 = float4(ResultTex[pos + int2(-1, 1)].r,
                       ResultTex[pos].r,
                       ResultTex[pos + int2(1, 1)].r,
                       ResultTex[pos + int2(0, 2)].r );
    
    float4 alpha = saturate(mul(inverse(R), r));
    
    float sum = alpha[0] + alpha[1] + alpha[2] + alpha[3];
    
    float4 alpha1, alpha2;
    
    if (sum == 0.0)
    {
        alpha1 = float4(0.5, 0, 0.5, 0);
        alpha2 = float4(0, 0.5, 0, 0.5); //Bilinear weights
    }
    else
    {
        alpha /= sum;
        
        alpha1 = alpha;
        alpha2 = alpha;
    }
    
    float result1 = dot(alpha1, n1);
    float result2 = dot(alpha2, n2);
    
    ResultTex[id.xy * 2 + int2(1, 0)] = result1;
    ResultTex[id.xy * 2 + int2(0, 1)] = result2;
}